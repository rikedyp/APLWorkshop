---
layout: workshop
title: Going Deeper
back: /course/day1/8
next: /course/day2/tools
---

### Day 2
Welcome back. At the end of the last section we saved our workspace. If you would like to continue to save your work as you go, please:
1. Load your previously saved workspace
1. Create a namespace called 'Day2' and change into that namespace.

You are of course welcome to simply proceed through each section with a `)CLEAR` workspace.

### Depth
We introduced *stranding* to show how it formed vectors before the application of dyadic functions, for example:

```APL
      2 + 2 2 + 2   
      2 + 2 (2 + 2)
      (2 + 2) 2 + 2
```

Stranding is a useful way to form arrays. Generally, arrays separated by spaces form vectors. 

```APL
      2 3⍴'DY' 'AL' 'OG'
      bnm←2 3⍴⍬
      bcm←3 2⍴''
      'a' 'b' 'c'
      mixed←3 3⍴1 2 3 'a' 'b' 'c'
      mixed2←3 3⍴1 2 3 'abc'
      3 1⍴mixed enm ecm
      ⍪mixed enm ecm
      ↑mixed enm ecm
      mixed,ecm
      mixed⍪ecm
      mixed⍪enm
      3 3⍴mixed,ecm
      3 3⍴mixed enm,ecm
```

A simple scalar is a rank-0 array which contains itself as its value.

```APL
      'a'≡⊃'a'   ⍝ The disclose of a simple scalar is itself
      42≡⊂42     ⍝ As is the enclose
```

1. When is stranding `a b` equivalent to catenation `a,b`?

### Primitive Loops

Experiment with the following expressions to determine what the each operator `¨` does.

```APL
      's',¨'ong' 'ink' 'and'
      'lph',¨'ong' 'ink' 'and'
      (1 2)(2 2)(3 1)⍴¨3 4 5
      2 2∘⍴¨3 4 5
```

The next number in the Fibonacci sequence is the sum of the previous two numbers. Examine this version using the `⍣` power operator.

```APL
      {⊃{(+/⍵),⊃⍵}⍣⍵⊢1}¨⍳5
```

### Word Problems

We are going to do some text processing on a dictionary of words. 

If you have access to the internet, the following expressions will download a text file dictionary (917kB in size) and store it as a nested vector of character vectors named `words`.

```APL
      ]Load HttpCommand
      words ← (⎕UCS 10) {(⍺≠⍵)⊆⍵} (HttpCommand.Get'https://tinyurl.com/y7asendy').Data
```

If you have the file on your computer (maybe you were given it on a USB drive, for example) then you can load it into 

Now answer these questions about palindromes.

A palindrome is the same when reversed. For example, *racecar* is a palindrome but *racecat* is not.

1. How many palindromes are there in `words`?

1. Which palindrome in `words` is the longest?

### An introduction to an introduction to an introduction to an introduction to an int...

Try the following dfns with various numeric arguments. Which symbol refers to the function itself? Which symbol separates statements? Which symbol represents a condition?

```APL
      {⍺←1 1 ⋄ ⍵=2:⍺ ⋄ (⍺,(+/¯2↑⍺))∇⍵-1}
```

Give the function an appropriate name.

If a function gets stuck in an infinite loop, use `Action → Interrupt` in the menu. You can also use the key combination `Ctrl+Break` to interrupt a running function.

1. Write the shortest dfn which causes infinite recursion.

1. Write the shortest dfn which causes infinite recursion unless its argument is `0`.

1. The factorial function multiplies integers up to `⍵`. Write the factorial function as a dfn called `Factorial`. Use the primitive `!` factorial function to check your solution. 

1. Write the factorial function as a reduction (an expression which includes `f/` for some function `f`).

### A Sort of Detour
```APL
      NSort←{0=⍴⍵:⍬⋄(U/⍵),∇(~U←⍵=⌊/⍵)/⍵}
```

```APL
      ↑'Bracken' 'Dolphin' 'Saucer' 'Magnet' 'Floop'
```
- `TSort←{⍺[((⍴⍵)[2])S ⍺⍳⍵]}`
- `S←{⍺=0:⍵ ⋄ (⍺-1)S ⍵[⍋⍵[;⍺];]}`

What do the following expressions tell you about the `⍋` grade-up and `⍒` grade-down functions on high-rank arrays?

```APL
      ⍋4 2 2⍴∊1 2 3 4,¨?4⍴(⊂3⍴10)
      ⍒4 2 2⍴'ABCD',¨{⎕A[⍵]}¨?4⍴(⊂3⍴10)
```

#### Rank Practice

1. Model Each
    Write models of the following functions using rank `⍤` instead of each `¨`. That is, write `IotaEach`, `SumEach` and `ReshapeEach` as dfns which use the rank operator.
    ```APL
          IotaEach 1 2 3             ⍝ ⍳¨1 2 3
    ┌─┬───┬─────┐
    │1│1 2│1 2 3│
    └─┴───┴─────┘
          SumEach 2 3⍴ IotaEach ⍳6   ⍝ +/¨2 3⍴⍳¨⍳6
     1  3  6
    10 15 21

    ```

1. Split k-cells 
    The *split* function (monadic `↓`) splits an array of rank ≥2 by rows, returning an array of shape `¯1↓⍴⍵`. Use *enclose* `⊂` with the rank operator `⍤` to create a function `Split` which always splits an array into a nested vector of the major cells of `⍵`.

    ```APL
          Split 3 2 2 3⍴⍳9
      ┌─────┬─────┬─────┐
      │1 2 3│4 5 6│7 8 9│
      │4 5 6│7 8 9│1 2 3│
      │     │     │     │
      │7 8 9│1 2 3│4 5 6│
      │1 2 3│4 5 6│7 8 9│
      └─────┴─────┴─────┘
    ```