# Solutions and Explanations 1

## Problem Set 1

### 1.1.1

```APL
      ×/⍳12          ⍝ 1.1.1.a
479001600
      +/(⍳17)*2      ⍝ 1.1.1.b
1785
      +/2×⍳100       ⍝ 1.1.1.c
10100
      +/(2×⍳100)-1   ⍝ 1.1.1.d
10000      
```

1.1.1.e) Expressions evaluate from right to left and functions have long right scope and short left scope. The order of operations can be made explicit with parentheses.

`84 - (12 - (1 - (13 - (28 - (9 - (6 - 15))))))`

### 1.1.2

```APL
      +/(⍳467)*2        ⍝ 1.1.2.a
34058310
      +/(¯1+2×⍳812)*2   ⍝ 1.1.2.b
713849500
      +/(⍳68)*3         ⍝ 1.1.2.c
5503716
```

### 1.1.3
In these examples, the arithmetic is more or less trivial. However, the goal is for students to discover a mathematical way of finding the length of a vector as they have not yet been introduced to shape `⍴` or tally `≢`.

```APL
      +/⎕AVU                                 ⍝ 1.1.3.a
646112
      ×/⎕AVU                                 ⍝ 1.1.3.b
0
      +/⎕AVU=⎕AVU ⋄ +/⎕AVU÷⎕AVU ⋄ +/⎕AVU*0   ⍝ 1.1.3.c
256
256
256
      (+/⎕AVU)÷+/⎕AVU=⎕AVU                   ⍝ 1.1.3.d
2523.875
```