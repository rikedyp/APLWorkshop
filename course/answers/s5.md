# Solutions and Explanations 5

### Reading and understanding expressions

The example expression descriptions in section 5 suggest an emphasis on the structure of data (shape) and what is be represented by the data.

There are a couple of techniques which you should be aware of which can help when faced with unfamiliar expressions.

##### Black Box
Execute the expression with different arguments and see what you get. 

This can broaden your understanding of the actual APL technique used, even though an accompanying comment might describe in natural language what the expression is intended for.

You might notice, for example, that the first example given is slightly misleading.

`{2×⍳⍵}   ⍝ Even integers from 2 to 2×⍵ inclusive`

This description is only true for single number arguments. Try the expression with a vector argument and you will see that a more technically accurate description is as follows.

`{2×⍳⍵}   ⍝ Even indices of an array of shape 2×⍵`

However, this description is more like a literal translation from APL primitives to English. It is often more useful to describe the intended use (list integers) than a more complete technical description, which can be determined from reading the code itself.

##### Take it apart, put it back together
The way an expression evaluates was discussed in section 1. The rules are often stated as "right-to-left" execution, but I find this term more confusing than helpful.

Parenthesising expressions can help improve your understanding. However, for a long expression it is a good idea to take pieces off, break down the function and try to understand each phrase in the expression before putting it back together.

Here is an example.

```APL
      ⍸2=+⌿0=∘.|⍨⍳100   ⍝ Primes up to 100
```

Using the black box method and also from the description, we understand that this somehow returns prime numbers from 1 to 100. 

Now we go from right to left and piece together the function, experimenting with any unfamiliar pieces to gain an understanding and then putting them back together.

```APL
      ⍳15
1 2 3 4 5 6 7 8 9 10 11 12 13 14 15
      ⍨⍳15
SYNTAX ERROR
      ⍨⍳15
      ∧
      ∘.|⍨⍳15
0 0 0 0 0 0 0 0 0  0  0  0  0  0 0
1 0 1 0 1 0 1 0 1  0  1  0  1  0 1
1 2 0 1 2 0 1 2 0  1  2  0  1  2 0
1 2 3 0 1 2 3 0 1  2  3  0  1  2 3
1 2 3 4 0 1 2 3 4  0  1  2  3  4 0
1 2 3 4 5 0 1 2 3  4  5  0  1  2 3
1 2 3 4 5 6 0 1 2  3  4  5  6  0 1
1 2 3 4 5 6 7 0 1  2  3  4  5  6 7
1 2 3 4 5 6 7 8 0  1  2  3  4  5 6
1 2 3 4 5 6 7 8 9  0  1  2  3  4 5
1 2 3 4 5 6 7 8 9 10  0  1  2  3 4
1 2 3 4 5 6 7 8 9 10 11  0  1  2 3
1 2 3 4 5 6 7 8 9 10 11 12  0  1 2
1 2 3 4 5 6 7 8 9 10 11 12 13  0 1
1 2 3 4 5 6 7 8 9 10 11 12 13 14 0
      ∘.×⍨⍳3
1 2 3
2 4 6
3 6 9
      0=∘.|⍨⍳15
1 1 1 1 1 1 1 1 1 1 1 1 1 1 1
0 1 0 1 0 1 0 1 0 1 0 1 0 1 0
0 0 1 0 0 1 0 0 1 0 0 1 0 0 1
0 0 0 1 0 0 0 1 0 0 0 1 0 0 0
0 0 0 0 1 0 0 0 0 1 0 0 0 0 1
0 0 0 0 0 1 0 0 0 0 0 1 0 0 0
0 0 0 0 0 0 1 0 0 0 0 0 0 1 0
0 0 0 0 0 0 0 1 0 0 0 0 0 0 0
0 0 0 0 0 0 0 0 1 0 0 0 0 0 0
0 0 0 0 0 0 0 0 0 1 0 0 0 0 0
0 0 0 0 0 0 0 0 0 0 1 0 0 0 0
0 0 0 0 0 0 0 0 0 0 0 1 0 0 0
0 0 0 0 0 0 0 0 0 0 0 0 1 0 0
0 0 0 0 0 0 0 0 0 0 0 0 0 1 0
0 0 0 0 0 0 0 0 0 0 0 0 0 0 1
      +⌿0=∘.|⍨⍳15
1 2 2 3 2 4 2 4 3 4 2 6 2 4 4
      +⌿1 2 3
6
      +⌿2 3⍴⍳6
5 7 9
      2=+⌿0=∘.|⍨⍳15
0 1 1 0 1 0 1 0 0 0 1 0 1 0 0
      ⍸2=+⌿0=∘.|⍨⍳15
2 3 5 7 11 13
```

Take your time to experiment so that you can naturally develop an intuition. In time what were once cryptic mysterious jumbles of alien symbols will be a source of comfort in trying times.