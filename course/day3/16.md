---
layout: workshop
title: Out in the Wild
back: /course/day3/15
next: /course/day3/resources
---

Much of what is presented in this course is what is called *modern APL* in that it contains extensions to the [original mathematical notation](https://www.jsoftware.com/papers/tot.htm). The term "modern APL" generally means APL implementations with some form of general nested arrays. Dyalog includes some primitives which first appeared in the [J language](https://aplwiki.com/wiki/J) before they were in any APL (true?).

## Branch
Despite long and widespread use in many programming languages, `:If :Else`-style control structures are a relatively recent introduction to some APLs. Early on, the only way to control the flow of execution in APL was to using the branching arrow `→`.

```APL
      →ln             ⍝ Go to integer line number ln
      →0              ⍝ Return from function
      →⍬              ⍝ Go to next line
      →condition/ln   ⍝ If condition is true (1), go to line ln

    ∇  r←BFac n   ⍝ Branching Factorial
[1]   ⍝ {⍵=1:⍵ ⋄ ⍵×∇ ⍵-1}              
[2]    →(n=1)/4                        
[3]    r←n×BFac n-1 ⋄ →0               
[4]    r←n                             
    ∇   
```

Keeping track of line numbers in this way would be a hassle for large programs.

```APL
    ∇  r←BFacL n   ⍝ Branching Factorial
[1]                ⍝ with Label         
[2]   ⍝ {⍵=1:⍵ ⋄ ⍵×∇ ⍵-1}               
[3]    →(n=1)/end                       
[4]    r←n×BFac n-1 ⋄ →0                
[5]   end:r←n                           
    ∇    
```

```APL
    ∇  r←BFacG n   ⍝ Branching Factorial
[1]                ⍝ with GoTo          
[2]   ⍝ {⍵=1:⍵ ⋄ ⍵×∇ ⍵-1}               
[3]    :If n=1                          
[4]        :GoTo end                    
[5]    :EndIf                           
[6]    r←n×BFac n-1                     
[7]    :Return                          
[8]   end:r←n                           
    ∇   
```

```APL
    ∇  r←BFacI n   ⍝ Branching Factorial
[1]                ⍝ with If            
[2]   ⍝ {⍵=1:⍵ ⋄ ⍵×∇ ⍵-1}               
[3]    :If n=1                          
[4]        r←n                          
[5]    :Else                            
[6]        r←n×BFac n-1                 
[7]    :EndIf                           
    ∇  
```

## The Axis Operator
Before the rank operator was invented (cite HOPL4, section 3.1, page 34), certain functions had comparable behaviour in conjunction with *the axis operator*. However, the axis operator is not a true operator; it is not general, does not fit the standard function-operator syntax and does not work with user-defined functions.

However, there are some useful applications of the axis operator which are handy to know. Some can only be replicated with combinations of both `⍤` rank and `⍉` transpose, for example.

```APL
      ,[1 2]2 3 4⍴⎕A   ⍝ Merge first two axes
      ,[2 3]2 3 4⍴⎕A   ⍝ Merge last two axes

      (2 3⍴⍳6),[1]2 3⍴⎕A     ⍝ Catenate
      (2 3⍴⍳6),[0.5]2 3⍴⎕A   ⍝ Laminate
```

Knowledge of the axis operator is required for anyone maintaining code which uses it, and it should be used to retain style unless all uses are to be replaced systematically with the rank operator.

